domain: "Camelot";
goal: !alive(Peter) | (location(Peter) = Home & location(Medicine) = Peter);
//goal: !alive(Bandit);

// Types
type location;
type place extends location;
type container extends location;
type character extends location, agent;
type item;
type coin extends item;
type merchandise extends item;
type weapon extends item, merchandise;

// Entities
character : Peter;
character : Merchant;
character : Guard;
character : Bandit;
place :	Home;
place :	Market;
place :	Camp;
coin : PeterCoin;
coin : BanditCoin;
merchandise : Medicine;
weapon : MerchantSword;
weapon : GuardSword;
weapon : BanditSword;
container : Chest;

// Properties
boolean	:	path(place, place);
place :		location(character);
place :		location(container);
location :	location(item);
boolean :	alive(character) = True;
boolean :   knowsCriminal(character, character);
boolean :	attacked(character);
weapon :	wielding(character);
boolean :   isGuard(character);
boolean :	isMerchant(character);
boolean :   isBandit(character);
boolean : 	criminalAtLarge();

// Intentions
intends(Peter, location(Medicine) = Peter);
intends(Peter, location(Peter) = Home);
intends(Merchant, location(Merchant) = Market);
intends(Guard, criminalAtLarge() = False);
intends(Bandit, location(PeterCoin) = Bandit);
intends(Bandit, location(BanditCoin) = Bandit);
intends(Bandit, location(Medicine) = Bandit);

// State
location(Peter) = Home;
location(Merchant) = Market;
location(Guard) = Market;
location(Bandit) = Camp;
location(Chest) = Camp;
location(PeterCoin) = Peter;
location(BanditCoin) = Chest;
location(Medicine) = Merchant;
location(MerchantSword) = Merchant;
location(GuardSword) = Guard;
location(BanditSword) = Bandit;
path(Home, Market);
path(Market, Home);
path(Market, Camp);
path(Camp, Market);
path(Home, Camp);
path(Camp, Home);
isGuard(Guard) = True;
isMerchant(Merchant) = True;
isBandit(Bandit) = True;
criminalAtLarge() = True;
wielding(Guard) = GuardSword;
wielding(Bandit) = BanditSword;
wielding(Merchant) = MerchantSword;
alive(Guard) = True;
alive(Merchant) = True;
alive(Peter) = True;
alive(Bandit) = True;

// Actions
action walk(character character*, place from, place to){
	precondition: (
		location(character) = from & 
		path(from, to) &
		alive(character)
	);
	effect: (
		location(character) = to
	);
}

action buyMedicine(character character*, merchandise item, coin coin){
	precondition: (
		isMerchant(character) = False &
		location(item) = Merchant &
		location(coin) = character &
		location(character) = Market &
		location(Merchant) = Market &
		isBandit(character) = False
	);
	effect: (
		location(item) = character &
		location(coin) = Merchant
	);
}

action buyWeapon(character character*, weapon weapon, coin coin){
	precondition: (
		isMerchant(character) = False &
		location(weapon) = Merchant &
		location(coin) = character &
		location(character) = Market &
		location(Merchant) = Market &
		isBandit(character) = False
	);
	effect: (
		location(weapon) = character &
		location(coin) = Merchant &
		wielding(character) = weapon &
		wielding(Merchant) = Null
	);
}

action reportCriminal(character character*, character criminal, place place){
	precondition: (
		isGuard(character) = False &
		isGuard(criminal) = False &
		criminal != character &
		knowsCriminal(character, criminal) &
		location(character) = place &
		location(Guard) = place
	);
	effect: (
		knowsCriminal(Guard, criminal) &
		criminalAtLarge() = True
	);
}

action steal(character character*, character victim, item item, weapon weapon, place place){
	precondition: (
		isGuard(character) = False &
		victim != character &
		item != weapon &
		alive(character) &
		alive(victim) &
		location(character) = place &
		location(victim) = place &
		location(item) = victim &
		wielding(character) = weapon &
		wielding(victim) = Null
	);
	effect: (
		location(item) = character &
		knowsCriminal(victim, character)
	);
}

action attack(character attacker*, character target, weapon weapon, place place){
	precondition: (
		isGuard(attacker) = False &		
		target != attacker &
		location(attacker) = place &
		location(target) = place &
		location(weapon) = attacker &
		wielding(attacker) = weapon &
		wielding(target) = Null &
		alive(attacker) &
		alive(target)
	);
	effect: (
		!alive(target) &
		knowsCriminal(Guard, attacker) &
		criminalAtLarge() = True
	);
}

action apprehend(character attacker*, character target, weapon weapon, place place){
	precondition: (
		isGuard(attacker) = True &
		knowsCriminal(attacker, target) &
		target != attacker &
		location(attacker) = place &
		location(target) = place &
		location(weapon) = attacker &
		wielding(attacker) = weapon &
		alive(attacker) &
		alive(target)
	);
	effect: (
		!alive(target) &
		criminalAtLarge() = False
	);
}

action takeFromContainer(character character*, item item, container container, place place){
	precondition: (
		location(character) = place & 
		location(container) = place &
		location(item) = container &
		alive(character)
	);
	effect: (
		location(item) = character
	);
}

action loot(character character*, character body, item item, place place){
	precondition: (
		location(character) = place &
		location(body) = place &
		alive(body) = False &
		location(item) = body &
		alive(character)
	);	
	effect: (
		location(item) = character
	);
}

//action trade(character a*, character b*, item itemA, item itemB, place place){
//	precondition: (
//		location(a) = place &
//		location(b) = place &
//		location(itemA) = a &
//		location(itemB) = b
//	);
//	effect: (
//		location(itemA) = b &
//		location(itemB) = a
//	);
//}

//axiom merchantWillTrade(character c, item cItem, item mItem){
//	precondition: (
//		location(c) = Market &
//		!criminal(c) &
//		location(cItem) = c &
//		location(mItem) = Merchant &
//		valuable(cItem) &
//		!intends(Merchant, location(mItem) = c)
//	);
//	effect: (
//		intends(Merchant, location(mItem) = c) &
//		intends(Merchant, location(cItem) = Merchant)
//	);	
//}

//axiom guardWants(character c){
//	precondition: (
//		criminal(c) & location(c) = Market & !intends(Guard, !alive(c))
//	);
//	effect: (
//		intends(Guard, !alive(c))
//	);
//}

//axiom banditWants(item i){
//		precondition: (
//			valuable(i) & !intends(Bandit, location(i) = Chest)			
//		);
//		effect: (
//			intends(Bandit, location(i) = Chest)
//		);
//}
